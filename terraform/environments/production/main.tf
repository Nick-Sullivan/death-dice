terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "4.48.0"
    }
  }
  backend "s3" {
    bucket = "nicks-terraform-states"
    key    = "death_dice/production/terraform.tfstate"
    region = "ap-southeast-2"
  }
}

locals {
  prefix            = "DeathDice"
  auth_callback_url = "https://100percentofthetimehotspaghetti.com/dice.html"
  auth_domain       = lower(local.prefix)
  tags = {
    Project = "Death Dice"
  }
}

provider "aws" {
  region = "ap-southeast-2"
  default_tags {
    tags = local.tags
  }
}

# Create an API with a URL, to be populated later

module "api_gateway_shell" {
  source = "./../../modules/api_gateway_shell"
  name   = local.prefix
}

# Create a database to store game state

module "database" {
  source = "./../../modules/database"
  prefix = local.prefix
}

# Create the lambdas that will interact with the database

module "lambdas" {
  source        = "./../../modules/lambdas"
  prefix        = local.prefix
  lambda_folder = "${path.root}/../../../lambda"
  table_arn     = module.database.table_arn
  gateway_url   = module.api_gateway_shell.gateway_url
}

# Populate the API so it will trigger the lambdas

module "api_gateway_integration" {
  source        = "./../../modules/api_gateway_integration"
  lambdas       = module.lambdas.lambdas
  websocket_id  = module.api_gateway_shell.websocket_id
  websocket_arn = module.api_gateway_shell.websocket_arn
}

# Create a dashboard for observing correct behaviour

module "cloudwatch" {
  source  = "./../../modules/cloudwatch"
  name    = local.prefix
  project = local.tags.Project
}

# Authenticate users

module "cognito" {
  source       = "./../../modules/cognito"
  name         = local.prefix
  callback_url = local.auth_callback_url
  domain       = local.auth_domain
}

# Save output for use website/app

resource "local_file" "website" {
  content  = <<-EOT
    // This file is automatically generated by terraform
    const clientId = "${module.cognito.client_id}"
    const gatewayUrl = "${module.api_gateway_shell.gateway_url}"
    const authDomain = "${local.auth_domain}"
    const authRedirectUrl = "${local.auth_callback_url}"
  EOT
  filename = "../../../website/js/terraform.js"
}

resource "local_file" "flutter" {
  content  = <<-EOT
    # This file is automatically generated by terraform
    WEBSOCKET_URL="${module.api_gateway_shell.gateway_url}"
    COGNITO_USER_POOL="${module.cognito.user_pool_id}"
    COGNITO_CLIENT_ID="${module.cognito.client_id}"
  EOT
  filename = "../../../flutter/.env"
}
